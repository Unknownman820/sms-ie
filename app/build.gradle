/*
 * SMS Import / Export: a simple Android app for importing and exporting SMS and MMS messages,
 * call logs, and contacts, from and to JSON / NDJSON files.
 *
 * Copyright (c) 2021-2025 Thomas More
 *
 * This file is part of SMS Import / Export.
 *
 * SMS Import / Export is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * SMS Import / Export is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with SMS Import / Export.  If not, see <https://www.gnu.org/licenses/>
 *
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 35

    defaultConfig {
        applicationId "com.github.tmo1.sms_ie"
        minSdkVersion 19
        targetSdkVersion 35
        versionCode 107
        versionName "2.4.1"
        base.archivesName = applicationId + '-v' + versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        /* The following 'debug {}' stanza is necessary to avoid hitting the 64K reference limit:
         https://developer.android.com/build/multidex
         Minification can, however, slow down build times, and Android Studio warns about this.
         This stanza is only necessary for building debug builds from the command line, or when
         deploying to systems with API level < 21; when deploying from Android Studio to systems
         with API level >= 21, multidex is enabled by default and the stanza can be deleted for
         faster build times:
         https://developer.android.com/build/multidex#mdex-on-l */
        debug {
            // https://stackoverflow.com/a/55785351
            postprocessing {
                removeUnusedCode true
                obfuscate false
                optimizeCode true
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    // Based on https://developer.android.com/build/build-variants#flavor-dimensions
    flavorDimensions = ["api"]
    productFlavors {
        standard {
            dimension "api"
            minSdkVersion 21
            versionCode 10000 + android.defaultConfig.versionCode
            versionNameSuffix "-standard"
        }
        legacy {
            dimension "api"
            minSdkVersion 19
            versionNameSuffix "-legacy"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += ['DebugProbesKt.bin']
        }
    }
    namespace 'com.github.tmo1.sms_ie'
    buildFeatures {
        buildConfig true
    }

}

dependencies {

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    standardImplementation 'androidx.core:core-ktx:1.15.0'
    standardImplementation 'androidx.appcompat:appcompat:1.7.0'
    standardImplementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    standardImplementation 'androidx.work:work-runtime-ktx:2.10.0'

    legacyImplementation 'androidx.core:core-ktx:1.13.1'
    legacyImplementation 'androidx.appcompat:appcompat:1.6.1'
    legacyImplementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    legacyImplementation 'androidx.work:work-runtime-ktx:2.9.1'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

}
